buildscript {
    def springBootVersion = '2.3.1.RELEASE'
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:2.4.0"
    }
}

wrapper {
    gradleVersion = '5.6.4'
}

allprojects {
    version = '0.0.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation ('org.springframework.boot:spring-boot-starter-web') {
            exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
        }
        implementation 'com.google.code.gson:gson'
        implementation 'org.projectlombok:lombok'
        testImplementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(':picktime-core') {
    dependencies {
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    }
}

project(':picktime-admin-webapp') {

    dependencies {
        implementation project(':picktime-core-datasource-mybatis')
        implementation 'mysql:mysql-connector-java'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'com.github.zhanhb:thymeleaf-layout-dialect:2.4.1'
        implementation 'org.springframework.boot:spring-boot-starter-undertow'
        //implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
        //implementation 'org.springframework.boot:spring-boot-starter-security'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
}

project(':picktime-webapi') {
    apply plugin: 'org.asciidoctor.jvm.convert'

    dependencies {
        implementation project(':picktime-core-datasource-mybatis')
        implementation 'mysql:mysql-connector-java'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        implementation 'org.springframework.boot:spring-boot-starter-undertow'
    }
}

project(":picktime-web-crawler") {

    dependencies {
        implementation project(':picktime-core-datasource-mybatis')
        implementation files('libs/instagram-crawler.jar')
        implementation 'mysql:mysql-connector-java'
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation 'org.springframework.boot:spring-boot-starter-undertow'
        testImplementation 'org.springframework.batch:spring-batch-test'
    }
}

project(":picktime-core-datasource-mybatis") {
    apply plugin: 'java-library'

    dependencies {
        api 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3'
    }
}

/*project(":picktime-core-datasource-jpa") {
    apply plugin: 'java-library'

    dependencies {
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}*/